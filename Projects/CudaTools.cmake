# Author: Ali Uneri
# Date: 2013-04-30

set(EP_REQUIRED_PROJECTS CUDA)
set(EP_OPTIONAL_PROJECTS MATLAB SWIG)

list(APPEND EP_PATH @BINARY_DIR@/Applications/@INTDIR@)
list(APPEND EP_LIBRARYPATH @BINARY_DIR@/Interfaces/@INTDIR@)

if(PROJECTS_MATLAB)
  list(APPEND EP_REQUIRED_PROJECTS MATLAB)
  list(APPEND EP_MATLABPATH @BINARY_DIR@/Interfaces/Matlab)
endif()
if(PROJECTS_SWIG)
  list(APPEND EP_REQUIRED_PROJECTS SWIG)
  list(APPEND EP_PYTHONPATH @BINARY_DIR@/Interfaces/Python)
endif()

cmt_end_definition()
# -----------------------------------------------------------------------------

set(EP_CMAKE_ARGS
  -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
  -DCudaTools_BUILD_MATLAB:BOOL=${PROJECTS_MATLAB}
  -DCudaTools_BUILD_PYTHON:BOOL=${PROJECTS_SWIG}
  -DCudaTools_BUILD_TESTING:BOOL=${CMT_BUILD_TESTING})

if(PROJECTS_MATLAB AND CMT_BUILD_TESTING)
  list(APPEND EP_CMAKE_ARGS
    -DCudaTools_MATLAB_EXECUTABLE:FILEPATH=${CMT_MATLAB_EXECUTABLE})
endif()

if(PROJECTS_SWIG)
  list(APPEND EP_CMAKE_ARGS
    -DPYTHON_EXECUTABLE:FILEPATH=${CMT_PYTHON_EXECUTABLE}
    -DPYTHON_INCLUDE_DIR:PATH=${CMT_PYTHON_INCLUDE_DIR}
    -DPYTHON_LIBRARY:FILEPATH=${CMT_PYTHON_LIBRARY}
    -DSWIG_DIR:PATH=${CMT_SWIG_DIR}
    -DSWIG_EXECUTABLE:FILEPATH=${CMT_SWIG_EXECUTABLE}
    -DSWIG_VERSION:STRING=${CMT_SWIG_VERSION})
endif()

set(EP_SOURCE_DIR ${EP_OPTION_NAME}_SOURCE_DIR)
get_filename_component(PARENT_DIR ${PROJECT_SOURCE_DIR} PATH)
find_path(
  ${EP_SOURCE_DIR}
  NAMES CMakeLists.txt
  HINTS ${PARENT_DIR}/${EP_NAME}
  NO_DEFAULT_PATH)
if(NOT ${EP_SOURCE_DIR})
  message(FATAL_ERROR "Please specify ${EP_SOURCE_DIR}")
endif()

ExternalProject_Add(${EP_NAME}
  DEPENDS ${EP_REQUIRED_PROJECTS}
  # download
  DOWNLOAD_COMMAND ""
  # patch
  # update
  # configure
  SOURCE_DIR ${${EP_SOURCE_DIR}}
  CMAKE_ARGS ${EP_CMAKE_ARGS}
  # build
  BINARY_DIR ${PROJECT_BINARY_DIR}/${EP_NAME}-build
  # install
  INSTALL_COMMAND ""
  # test
  )
